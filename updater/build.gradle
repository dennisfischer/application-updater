/*
 * Copyright (c) 2013 Dennis Fischer.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0+
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 *
 * Contributors: Dennis Fischer
 */

def javaFxHome = "${System.properties['java.home']}";

apply plugin: 'java'
apply plugin: 'application'
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = "0.1";
mainClassName = 'de.chaosfisch.updater.Launcher'

configurations {
    //we do not want to have jfxrt.jar in the classpath when creating the jar,
    //therefore a seperate configuration is required
    providedCompile
    p200AntTask
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.0.9', 'com.google.code.gson:gson:2.2.2',
            'com.google.guava:guava:14+', 'com.google.inject:guice:3.0', 'com.google.inject.extensions:guice-assistedinject:3.0',
            'com.google.inject.extensions:guice-multibindings:3.0', 'commons-configuration:commons-configuration:+'
    compile files('lib/Undecorator.jar')

    p200AntTask files('lib/p200ant.jar')

    providedCompile files("$javaFxHome/lib/jfxrt.jar")
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "http://maven.mashape.com/releases" }
    maven { url "https://repo.eclipse.org/content/groups/releases" }
}


compileJava {
    //add required JavaFX libs to compile classpath
    sourceSets.main.compileClasspath += configurations.providedCompile
}

jar {
    archiveName = "Updater.jar"
}


task javaFXPackager(type: Exec) {
    commandLine = [
            'C:/Program Files/Java/jdk1.7.0_40/bin/javafxpackager.exe',
            '-createjar',
            '-srcdir',
            sourceSets.main.output.classesDir,
            '-outdir',
            jar.destinationDir,
            '-outfile',
            jar.archiveName,
            '-appclass',
            mainClassName,
            '-nocss2bin',
            '-classpath',
            configurations.runtime.collect { "lib/" + it.name }.join(' '),
            '-manifestAttrs',
            'Implementation-Title=Updater ' + version + ',Implementation-Version=' + version + ',Implementation-Vendor=Dennis Fischer aka CHAOSFISCH,' +
                    'Built-By=' + System.getProperty('user.name') + ',Built-Date=' + new Date() + ',Built-JDK=' + System.getProperty('java.version')
    ]
}

task copyTask(type: Copy) {
    from sourceSets.main.output.resourcesDir
    into sourceSets.main.output.classesDir
}

task distribute(type: Zip) {
    baseName 'updater'
    from(jar)
    into("lib") {
        from(project.configurations.runtime)
    }
}

distribute.dependsOn javaFXPackager
javaFXPackager.dependsOn copyTask